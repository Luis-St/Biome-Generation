plugins {
	id "java"
	id "org.openjfx.javafxplugin" version "0.1.0"
}

repositories {
	mavenCentral()
	maven {
		url "https://maven.luis-st.net/libraries"
	}
	maven {
		url "https://libraries.minecraft.net"
	}
}

javafx {
	version = "${JavaFx}"
	modules = ["javafx.controls"]
}

dependencies {
	// Local
	implementation "org.controlsfx:ControlsFx:${ControlsFx}" // Fx
	implementation "net.luis:LUtils:${LUtils}" // Utility
	implementation "net.luis:FxUtils:${FxUtils}" // Fx Utility
	// Google
	implementation "com.google.guava:guava:${GoogleGuava}" // Utility
	implementation "com.google.code.gson:gson:${GoogleGson}" // Json
	// Apache
	implementation "org.apache.logging.log4j:log4j-api:${Log4jAPI}" // Logging
	implementation "org.apache.logging.log4j:log4j-core:${Log4jCore}" // Logging
	implementation "org.apache.commons:commons-lang3:${ApacheLang}" // Utility
	implementation "commons-io:commons-io:${ApacheIO}" // IO
	// Other
	implementation "net.sf.jopt-simple:jopt-simple:${JOptSimple}" // Startup
	implementation "org.jetbrains:annotations:${JetBrainsAnnotations}" // Annotations
	implementation "it.unimi.dsi:fastutil:${UnimiFastutil}" // Utility
	implementation "com.mojang:datafixerupper:${MojangCodec}"// Console
}

tasks.register("displayHelp", JavaExec) {
	group = "runs"
	getMainClass().set("net.luis.generation.Main")
	classpath = sourceSets.main.runtimeClasspath
	args("--help")
	jvmArgs("--module-path", classpath.asPath, "--add-modules", "javafx.controls")
}

tasks.register("runUi", JavaExec) {
	group = "runs"
	getMainClass().set("net.luis.generation.Main")
	classpath = sourceSets.main.runtimeClasspath
	args("--ui", "--dev", "--config", file("src/main/resources/config.json"))
	jvmArgs("--module-path", classpath.asPath, "--add-modules", "javafx.controls")
}

tasks.register("runImage", JavaExec) {
	group = "runs"
	getMainClass().set("net.luis.generation.Main")
	classpath = sourceSets.main.runtimeClasspath
	args("--ui", "--dev", "--config", file("src/main/resources/config.json"), "--output", System.getProperty("user.home") + "/Desktop", "--size", 10000)
	jvmArgs("--module-path", classpath.asPath, "--add-modules", "javafx.controls")
}

tasks.register("generateData", JavaExec) {
	group = "runs"
	getMainClass().set("net.luis.generation.Main")
	classpath = sourceSets.main.runtimeClasspath
	args("--data", "--dev", "--config", file("src/main/resources/config.json"), "--output", file("src/main/generated/"))
	jvmArgs("--module-path", classpath.asPath, "--add-modules", "javafx.controls")
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

jar {
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}
